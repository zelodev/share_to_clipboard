plugins {
    id 'com.android.application' version '8.11.0'
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.jetbrains.kotlin') {
            details.useVersion '1.8.22'
        }
    }
}

android {
    namespace = 'com.tengu.sharetoclipboard'
    compileSdk = 36

    defaultConfig {
        applicationId = "com.tengu.sharetoclipboard"
        minSdk = 21
        targetSdk = 36
        versionCode = 33
        versionName = "1.15.1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_24
        targetCompatibility JavaVersion.VERSION_24
    }
    
    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }

        debug {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lint {
        disable 'MissingTranslation'
    }

    if (System.getenv("signingStoreLocation") != null) {

        println "Found sign properties in gradle.properties! Signing buildâ€¦"

        signingConfigs {
            release {
                storeFile = file(System.getenv("signingStoreLocation"))
                storePassword = System.getenv("KSTOREPWD")
                keyPassword = System.getenv("KSTOREPWD")
            }
        }

        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-Xlint:deprecation'
    }
}

dependencies {
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.12.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.legacy:legacy-support-core-ui:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
}
